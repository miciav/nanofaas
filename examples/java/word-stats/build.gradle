plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'org.graalvm.buildtools.native' version "${graalvmBuildToolsVersion}"
    id 'java'
}

dependencies {
    implementation project(':function-sdk-java')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = 'word-stats.jar'
}

bootBuildImage {
    imageName = project.findProperty('functionImage') ?: 'nanofaas/java-word-stats:buildpack'
    def builderImage = (project.findProperty('imageBuilder') ?: System.getenv('IMAGE_BUILDER')
            ?: 'paketobuildpacks/builder-jammy-tiny:latest').toString()
    def runImageName = (project.findProperty('imageRunImage') ?: System.getenv('IMAGE_RUN_IMAGE')
            ?: 'paketobuildpacks/run-jammy-tiny:latest').toString()
    builder = builderImage
    runImage = runImageName
    // Target platform for cross-compilation (e.g., linux/amd64, linux/arm64)
    def targetPlatform = project.findProperty('imagePlatform') ?: System.getenv('IMAGE_PLATFORM')
    if (targetPlatform) {
        imagePlatform = targetPlatform
    }
    environment = ['BP_NATIVE_IMAGE': 'true']
}
