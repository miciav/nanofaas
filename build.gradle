plugins {
    id 'java'
}

allprojects {
    group = 'com.nanofaas'
    version = '0.7.1'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(Integer.parseInt(project.findProperty('javaVersion') ?: '21'))
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform {
            if (!project.hasProperty('runE2e')) {
                excludeTags 'inter_e2e'
            }
        }
    }
}

tasks.register('k8sE2e') {
    group = 'verification'
    description = 'Provision kind in Multipass, load images, and run K8sE2eTest.'
    dependsOn ':control-plane:k8sE2eTest'
}

tasks.register('k8sE2eVm', Exec) {
    group = 'verification'
    description = 'Run K8sE2eTest in an ephemeral Multipass VM with k3s.'
    commandLine 'bash', 'scripts/e2e-k8s-vm.sh'
}
